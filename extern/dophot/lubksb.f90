! Subroutine lubksb(a,n,np,indx,b)
! This subroutine solves the set of n linear equations A*X=B
! It needs to be used with the subroutine LUDCMP, since the matrix A and 
! the vector INDX need to be generated by this subroutine, which does 
! an LU decomposition of the original matrix A.
! B is input as the right-hand vector B, and returns with the solution vector X.
! To better understand this subroutine and the LU decomposition, 
! check section 2.3 in Numerical Recipes in Fortran.
 
subroutine lubksb(a,n,np,indx,b)

  real ::  a(np,np),b(n)
  integer :: indx(n)

  ii=0
  do i=1,n
     ll=indx(i)
     sum=b(ll)
     b(ll)=b(i)
     if (ii.ne.0)then
        do j=ii,i-1
           sum=sum-a(i,j)*b(j)
        end do
     else if (sum.ne.0.) then
        ii=i
     endif
     b(i)=sum
  end do
  do i=n,1,-1
     sum=b(i)
     if(i.lt.n)then
        do j=i+1,n
           sum=sum-a(i,j)*b(j)
        end do
     endif
     b(i)=sum/a(i,i)
  end do
  
end subroutine lubksb
