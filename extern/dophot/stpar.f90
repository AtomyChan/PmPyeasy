! The model used in DoPHOT for the surface-brightness profiles of stellar images
! consists of similar ellipses of the form
! I(x,y) = I0 * ( 1 + z**2 + beta4/2*(z**2)**2 + beta6/6*(z**2)**3 )**(-1) + Is
! where
! z**2 = 1/2 * (x**2/sigx**2 + 2*sigxy*x*y + y**2/sigy**2)
! and 
! x = x' - x0 ; y = y'-y0
! with the nominal center of the image at (x0,y0)
!
! A wide variety of PSFs can be generated by an appropiate choice of values.
! There are seven free parameters in this function that are transported 
! through the different functions and subroutines in an array of 7 elements:
! 1 - the background intensity Is (sky) 
! 2 - the central intensity I0
! 3 - the object center in x, x0
! 4 - the object center in y, y0
! 5 - the shape parameter sigx (actually, sigx**2)
! 6 - the shape parameter sigxy
! 7 - the shape parameter sigy (actually, sigy**2)
!
! To better understand this subroutine check section 4.2 in the DoPHOT paper 
! (Schechter, Mateo, and Saha 1993 PASP, 105, 1345).
!
!---------------------------------------------------------------------------
!---------------------------------------------------------------------------
! This file contains subroutines that work with the parameters of the above
! function:
!
! STPAR1
! STPAR567
! STPAR567TWO
! STPARAVG
! STPARRMS
!
!---------------------------------------------------------------------------
!---------------------------------------------------------------------------
!
! Function stpar1 (x,y,flags,nff,skyplanepar,npskyp,skyhubpar,npskyh,&
!     skymedarray,nfast,nslow,skymedstep)
! This function calculates the intensity of the sky at a given position.
! This subroutine assigns values to the analytic stellar function 
! parameter 1 (see explanation above).

function stpar1 (x,y,flags,nff,skyplanepar,npskyp,skyhubpar,npskyh,&
     skymedarray,nfast,nslow,iskymedstep)

  real :: skyplanepar(npskyp),skyhubpar(npskyh)
  real :: skymedarray(nfast/iskymedstep,nslow/iskymedstep)
  real :: xy(2),dummyp(npskyp),dummyh(npskyh)
  character(*) :: flags(nff)

  xy(1) = x
  xy(2) = y
  if(flags(2)(1:5).eq.'PLANE') &
      call skyfun_plane(xy,skyplanepar,npskyp,dum,dum,dummyp,stpar1)
  if(flags(2)(1:5).eq.'HUBBL') &
      call skyfun_hub(xy,skyhubpar,npskyh,dum,dum,dummyh,stpar1)
  if(flags(2)(1:5) .eq. 'MEDIA') & 
       stpar1 = SKYFUN_MED(xy,skymedarray,nfast,nslow,iskymedstep)

end function stpar1

!---------------------------------------------------------------------------
!
! Subroutine stpar567 (x,y,flags,nff,enuffvpsf,a5,a6,a7,npsffit,ava,npstar, 
!     > starpar)
! This subroutine assigns values to the analytic stellar function 
! parameters 5, 6, and 7 (see explanation above)

subroutine stpar567(x,y,flags,nff,enuffvpsf,a5,a6,a7,npsffit,ava,npstar,starpar)
  
  real :: starpar(npstar)
  real :: ava(npstar)
  real :: a5(npsffit),a6(npsffit),a7(npsffit),dummy(npsffit)
  real :: xy(2)
  character(*) :: flags(nff)
  logical :: enuffvpsf

  xy(1) = x
  xy(2) = y

!  Two options:
!  - If variable psf is on, determine the values for this location.
!  - If variable psf is off, take the average shape parameters.
  if((flags(11)(1:1).eq.'Y').and.(enuffvpsf)) then
     call surface(xy,a5,npsffit,dum,dum,dummy,starpar(5))
     call surface(xy,a6,npsffit,dum,dum,dummy,starpar(6))
     call surface(xy,a7,npsffit,dum,dum,dummy,starpar(7))
  else
     starpar(5) = ava(5)
     starpar(6) = ava(6)
     starpar(7) = ava(7)
  end if
  
end subroutine stpar567

!---------------------------------------------------------------------------
!
! Subroutine stpar567two (a,npstar,ix,iy, > starpar)
!
! This subroutine assigns values to the analytic stellar function 
! parameters 5, 6, and 7 AS IF they were parameters 2, 3, and 4.
! It is used only to check if the object observed is a blend of two stars.
! To better understand it, check subroutine shape.f90

subroutine stpar567two (a,npstar,ix,iy,starpar)

  real :: starpar(npstar),a(npstar)

  starpar(1) = a(1)                       
  do i = 0, 3, 3
     starpar(2+i) = a(2+i)             
     starpar(3+i) = a(3+i)+ix
     starpar(4+i) = a(4+i)+iy
  end do
      
end subroutine stpar567two

!---------------------------------------------------------------------------
!
! Subroutine stparavg(nstot,npstar,nsmax,itype,shadow,shaderr,lverb,>NSPERF,AVA)
!
! This subroutine calcutes weighted average values for the stellar parameters.

subroutine stparavg(nstot,npstar,nsmax,itype,shadow,shaderr,lverb,nsperf,ava)

  real :: shadow(npstar,nsmax),shaderr(npstar,nsmax)
  real :: parwt(npstar),parval(npstar),ava(npstar)
  integer :: itype(nsmax)

  nsperf = 0
  parwt = 0.
  parval = 0.
  if (nstot.eq.0) return
  do i = 1,nstot
     if (itype(i).eq.1) then
        nsperf = nsperf+1
        do j = 1,npstar
           if(shaderr(j,i).eq.0.) then
              write(13,*) 'shaderr = 0; i = ',i
              write(13,*)
              cycle
           end if
           parwt(j) = parwt(j)+1/shaderr(j,i)
           parval(j) = parval(j)+shadow(j,i)/shaderr(j,i)
        end do
     end if
  end do
  if (nsperf.gt.0) then
     do j = 1,npstar
        ava(j) = parval(j)/parwt(j)
     end do
  end if
  if(lverb.gt.10) then
     write(13,*) 'average values so far of 7 parameters for stars:'   
     write(13,*) ava
  endif

end subroutine stparavg

!---------------------------------------------------------------------------
!
! Subroutine stparrms(nstot,nsmax,npstar,starpar,itype,shadow,shaderr,ava,&
!     flags,nff,lverb,enuffvpsf,a5,a6,a7,npsffit, > STARPARRMS)
! This subroutine calculates the rms of the stellar parameters
! (just rms of parameters 5, 6, and 7 because these are the ones needed later)

subroutine stparrms(nstot,nsmax,npstar,starpar,itype,shadow,shaderr,ava,&
     flags,nff,lverb,enuffvpsf,a5,a6,a7,npsffit,starparrms)

  real :: stparres(npstar),stparreswt(npstar),starparrms(npstar),roots(npstar)
  real :: starpar(npstar,nsmax),shadow(npstar,nsmax),shaderr(npstar,nsmax)
  real :: a5(npsffit),a6(npsffit),a7(npsffit),ava(npstar),a(npstar)
  integer :: itype(nsmax)
  character(*) :: flags(nff)
  logical :: perfect,enuffvpsf


  stparres = 0.
  stparreswt = 0.
  starparrms = 0.
  do i = 1,nstot
     perfect = itype(i).eq.1
     if (perfect) then
        call stpar567 (starpar(3,i),starpar(4,i),flags,nff,&
             enuffvpsf,a5,a6,a7,npsffit,ava,npstar,a)
!:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
!   I have convinced myself that the uncertainty per residual squared is
!   2*delta*(o-c).  But if i use 1/this as a weight, accidental cancellations
!   give screwey results.  So we'll take the larger of o-c**2 and shaderr.
!:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        do j = 5,7
           res = (shadow(j,i)-a(j))**2
           wt = 1/(shaderr(j,i)*amax1(shaderr(j,i),res))
           stparreswt(j) = stparreswt(j) + wt
           stparres(j) = stparres(j) + wt*res
        end do
     end if
  end do
  do j = 5,7
     starparrms(j) = stparres(j)/stparreswt(j)
     if (starparrms(j).ge.0) then
        roots(j) = sqrt(starparrms(j))
     else
        if(lverb.gt.10) write(13,*) 'negative scatter :'
        if(lverb.gt.10) write(13,*) 'param# & scatter = ',j,starparrms(j)
        starparrms(j) = 0
        roots(j) = 0
     end if
  end do
  if(lverb.gt.10) write(13,*) 'scatter: ', roots

end subroutine stparrms
